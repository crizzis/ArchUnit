dependencies {
    compile project(path: ':archunit', configuration: 'shadow')
    compile dependency.guava
    compile dependency.junit

    testCompile dependency.log4j_api
    testCompile dependency.log4j_core
    testCompile dependency.log4j_slf4j
    testCompile dependency.mockito
    testCompile dependency.assertj
    testCompile(dependency.assertj_guava) {
        exclude module: 'assertj-core'
        exclude module: 'guava'
    }
    testCompile project(path: ':archunit', configuration: 'tests')
}

test {
    if (!project.hasProperty('allTests')) {
        useJUnit {
            excludeCategories 'com.tngtech.archunit.junit.Slow'
        }
    }
}

/* We only depend on archunit core and nothing else */
def configureDependenciesAfterShadow =  { pom ->
    pom.dependencies.removeAll {
        it.artifactId != 'archunit' || it.scope != 'compile'
    }
}
install.repositories.mavenInstaller.pom.whenConfigured configureDependenciesAfterShadow
uploadArchives.repositories.mavenDeployer.pom.whenConfigured configureDependenciesAfterShadow

configurations {
    /* Exclude archunit from the shadow jar */
    runtime.exclude module: ':archunit'
}
