buildscript {
    repositories {
        mavenCentral()
    }
}

subprojects {
    afterEvaluate {
        tasks.withType(Jar) {
            manifest {
                attributes(
                        'Built-By': "Gradle ${gradle.gradleVersion}",
                        'Built-Date': new Date().format('yyyy-MM-dd'),
                        'Specification-Title': createModuleDescription(app.name, project),
                        'Specification-Version': archiveVersion.get(),
                        'Specification-Vendor': company.name,
                        'Implementation-Title': "com.tngtech.${project.name.replace('-', '.')}",
                        'Implementation-Version': archiveVersion.get(),
                        'Implementation-Vendor': company.name,
                        'Issue-Tracker': app.urls.issues,
                        'Documentation-URL': app.urls.doc,
                        'Copyright': "${year} ${company.name}",
                        'License': app.license.name,
                        'Automatic-Module-Name': moduleName
                )
            }
        }
    }
}

productionProjects*.with {
    apply plugin: 'com.github.johnrengelman.shadow'

    shadowJar {
        archiveClassifier = ''

        exclude 'org/apache/**'
        exclude 'Log4j*'
        exclude 'module-info.class'

        relocate 'org.objectweb.asm', asmRelocationPackage
        relocate 'com.google.common', "${googleRelocationPackage}.common"
        relocate 'com.google.thirdparty', "${googleRelocationPackage}.thirdparty"
    }
    jar.enabled = false
    // we need this, since we want to publish the shadow JAR, not the original JAR -> https://docs.gradle.org/current/userguide/upgrading_version_6.html
    configurations {
        [apiElements, runtimeElements].each {
            it.outgoing.artifacts.removeIf { it.buildDependencies.getDependencies(null).contains(jar) }
            it.outgoing.artifact(shadowJar)
        }
    }

    task finishArchive(dependsOn: shadowJar) {}
    build.dependsOn(finishArchive)
}
