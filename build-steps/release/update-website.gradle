import java.nio.file.Files

apply from: scriptRelativePath(this, 'vcs-utils.gradle')

final String archunitGitRepo = 'TNG/ArchUnit.git'
final File checkoutDir = new File(project.buildDir, 'docs-update')

task checkoutArchUnitWebsite {
    doLast {
        gitHubClone(archunitGitRepo, checkoutDir, 'gh-pages')
    }
}

task copyArchUnitWebsite(dependsOn: checkoutArchUnitWebsite) {
    doLast {
        def cName = new File(checkoutDir, 'CNAME').toPath()
        def tempCName = Files.copy(cName, new File(File.createTempDir(), 'CNAME').toPath())
        def noGitFile = { !it.startsWith('.git') }
        checkoutDir.eachFileMatch(noGitFile) { it.directory ? it.deleteDir() : it.delete() }
        copy {
            from new File(rootDir, 'docs')
            into checkoutDir
        }
        Files.copy(tempCName, cName)
    }
}

task updateArchUnitWebsite(dependsOn: [checkoutArchUnitWebsite, copyArchUnitWebsite]) {
    ext.checkoutDir = checkoutDir
}
