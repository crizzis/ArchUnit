import java.nio.file.Files

def repositoryUrls = [
        tng: [
                snapshots: 'https://nexus.int.tngtech.com/content/repositories/snapshots',
                releases: 'https://nexus.int.tngtech.com/content/repositories/releases'
        ],
        sonatype: [
                snapshots: 'https://oss.sonatype.org/content/repositories/snapshots',
                releases: 'https://oss.sonatype.org/content/repositories/releases'
        ]
]
def createRepositoriesTag = { repoUrls ->
    """
    <repositories>
        <repository>
            <id>snapshots</id>
            <name>Snapshots</name>
            <url>${repoUrls.snapshots}</url>
        </repository>
        <repository>
            <id>releases</id>
            <name>Releases</name>
            <url>${repoUrls.releases}</url>
        </repository>
    </repositories>
"""
}

def getRepoUrls = {
    def repoUrls = repositoryUrls.get(it)
    assert repoUrls: "The configured -PmavenRepos=${it} don't exist"
    repoUrls
}

task prepareMavenTest {
    doLast {
        def pom = rootProject.file('archunit-maven-test/pom.xml')
        def pomBak = rootProject.file('archunit-maven-test/pom.xml.bak')
        pomBak.delete()
        Files.copy(pom.toPath(), pomBak.toPath())

        def repositories = project.hasProperty('mavenRepos') ?
                createRepositoriesTag(getRepoUrls(project.getProperty('mavenRepos'))) :
                ''

        pom.text = pom.text.
                replace('#{archunit.version}', "${version}").
                replace('#{repositories}', repositories)
    }
}

task executeMavenTest(type: Exec) {
    workingDir "$rootProject.projectDir/archunit-maven-test"

    commandLine 'mvn', 'clean', 'test'
}

task cleanUpMavenTest {
    doLast {
        def pom = rootProject.file('archunit-maven-test/pom.xml')
        def pomBak = rootProject.file('archunit-maven-test/pom.xml.bak')
        pom.delete()
        Files.copy(pomBak.toPath(), pom.toPath())
        pomBak.delete()
    }
}

cleanUpMavenTest.mustRunAfter executeMavenTest
executeMavenTest.finalizedBy cleanUpMavenTest
cleanUpMavenTest.mustRunAfter prepareMavenTest
executeMavenTest.mustRunAfter prepareMavenTest
task runMavenTest(dependsOn: [prepareMavenTest, executeMavenTest, cleanUpMavenTest])

productionProjects.with {
    executeMavenTest.mustRunAfter install
    executeMavenTest.mustRunAfter uploadArchives
}